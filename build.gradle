buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group 'org.taosha.android'
version '0.1.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 25
        versionCode 1
        versionName version
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    buildTypes {
        debug {
            externalNativeBuild {
                cmake {
                    cppFlags "-DSK_DEBUG"
                }
            }
        }
        release {
            externalNativeBuild {
                cmake {
                    cppFlags "-DSK_RELEASE"
                }
            }
        }
    }

    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
        armv7 {
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
    }
}

afterEvaluate {
    android.libraryVariants.each { variant ->
        if (variant.buildType.name == "release") {
            publishing.publications.create variant.flavorName, MavenPublication, {
                groupId group
                version version
                artifactId "$project.name"
                artifact(variant.outputs.outputFile.get(0)) {
                    classifier "$variant.flavorName"
                }
            }
        }
    }
    tasks.publishToMavenLocal.dependsOn tasks.assemble
    tasks.publish.dependsOn tasks.assemble
}

